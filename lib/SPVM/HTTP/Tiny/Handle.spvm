# Copyright (c) 2023 Yuki Kimoto
# MIT License

class HTTP::Tiny::Handle {
  version_from HTTP::Tiny;
  
  allow HTTP::Tiny;
  
  use Mojo::Message::Request;
  use Mojo::Message::Response;
  
  use Fn;
  use Re;
  use IO::Socket::INET;
  use Go::Context;
  
  # Enumerations
  private enum {
    READ_BUFFER_SIZE = 4086,
  }
  
  # Fields
  has inactivity_timeout : double;
  
  has socket : IO::Socket;
  
  private static method new : HTTP::Tiny::Handle ($options : object[]) {
    
    my $options_h = Hash->new($options);
    
    my $self = new HTTP::Tiny::Handle;
    
    # The inactivity_timeout field
    my $inactivity_timeout = $options_h->get_or_default("inactivity_timeout", 60.0)->(double);
    $self->{inactivity_timeout} = $inactivity_timeout;
    
    return $self;
  }
  
  private method connect : HTTP::Tiny::Handle ($ctx : Go::Context, $req : Mojo::Message::Request) {
    
    my $url = $req->url;
    my $scheme = $url->scheme;
    my $host = $url->host;
    my $port = $url->port;
    
    my $inactivity_timeout = $self->{inactivity_timeout};
    
    my $socket = (IO::Socket)undef;
    
    if ($scheme eq "http") {
      # Pass $ctx to connect
      $socket = IO::Socket::INET->new({
        PeerAddr  => $host,
        PeerPort  => $port,
        Timeout   => $inactivity_timeout,
      });
    }
    elsif ($scheme eq "https") {
      if (require IO::Socket::SSL) {
        # Pass $ctx to connect
        $socket = IO::Socket::SSL->new({
          PeerAddr  => $host,
          PeerPort  => $port,
          Timeout   => $inactivity_timeout,
        });
      }
      else {
        die "IO::Socket::SSL is required for HTTPS access.";
      }
    }
    else {
      die "The scheme $scheme is not supported.";
    }
    
    $self->{socket} = $socket;
    
    return $self;
  }
  
  private method write_request : void ($ctx : Go::Context, $req : Mojo::Message::Request) {
    
    my $socket = $self->{socket};
    
    my $start_line = $req->build_start_line;
    
    # Pass $ctx to write
    $socket->syswrite($start_line);
    
    my $header_lines = $req->build_headers;
    
    # Pass $ctx to write
    $socket->syswrite($header_lines);
    
    # Pass $ctx to write
    $socket->syswrite("\r\n");
  }
  
  private method read_response : Mojo::Message::Response ($ctx : Go::Context) {
    
    my $res = Mojo::Message::Response->new;
    
    my $handle = $self->{socket};
    
    while (1) {
      my $chunk = (mutable string)new_string_len &READ_BUFFER_SIZE;
      
      # Pass $ctx to read
      my $read_length = $handle->sysread($chunk);
      
      if ($read_length == 0) {
        $res->finish;
        last;
      }
      
      Fn->shorten($chunk, $read_length);
      
      $res->parse($chunk);
      
      if ($res->is_finished) {
        last;
      }
    }
    
    return $res;
  }
  
  method _find_CA_file : int () {
    return 0;
  }
}
