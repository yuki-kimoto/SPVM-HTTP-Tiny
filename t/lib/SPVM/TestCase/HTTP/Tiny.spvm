class TestCase::HTTP::Tiny {
  use HTTP::Tiny;
  use Fn;
  use Go;
  use Array;
  use IntList;
  use Go::Context;
  
  our $RESULT : IntList;
  
  static method test : int () {
    
    {
      # Use httpbin.org/get
      my $url = "https://httpbin.org/get";
      
      my $http = HTTP::Tiny->new;
      
      my $ctx = Go::Context->background;
      
      # Minimal GET request without custom headers
      my $res = $http->get($ctx, $url);
      
      my $content = $res->content;
      
      # Verify host connection by checking the URL in response body
      unless (Fn->contains($content, "httpbin.org")) {
        diag;
        return 0;
      }
      
      unless ($res->status == 200) {
        diag;
        return 0;
      }
      
      # --- Response Header Test ---
      # Get headers as a Hash object
      my $headers = $res->headers;
      
      # httpbin.org returns "application/json" as Content-Type
      # Note: We use lower-case keys for compatibility
      my $content_type = (string)$headers->get("content-type");
      unless (Fn->contains($content_type, "application/json")) {
        diag;
        return 0;
      }

      # Check another common header like "server"
      my $server = (string)$headers->get("server");
      if (Fn->length($server) == 0) {
        diag;
        return 0;
      }
    }
    
    {
      my $url = "https://httpbin.org/get";
      
      my $http = HTTP::Tiny->new;
      
      my $ctx = Go::Context->background;
      
      # Use request method explicitly for GET
      my $res = $http->request($ctx, "GET", $url);
      
      my $content = $res->content;
      
      # Verify host connection
      unless (Fn->contains($content, "httpbin.org")) {
        diag;
        return 0;
      }
      
      unless ($res->status == 200) {
        diag;
        return 0;
      }
    }
    
    return 1;
  }
  
  static method test_methods : int () {
    my $http = HTTP::Tiny->new;
    my $ctx = Go::Context->background;

=pod TODO:Temporarily disable HEAD method test due to hang issue in SPVM::Mojo::UserAgent

    # HEAD (Body must be empty)
    {
      my $res = $http->head($ctx, "https://httpbin.org/get");
      unless ($res->status == 200 && (Fn->length($res->content) == 0)) {
        diag;
        return 0;
      }
    }

=cut

    # POST
    {
      my $res = $http->post($ctx, "https://httpbin.org/post");
      unless ($res->status == 200 && Fn->contains($res->content, "httpbin.org/post")) {
        diag;
        return 0;
      }
    }

    # PUT
    {
      my $res = $http->put($ctx, "https://httpbin.org/put");
      unless ($res->status == 200 && Fn->contains($res->content, "httpbin.org/put")) {
        diag;
        return 0;
      }
    }

    # PATCH
    {
      my $res = $http->patch($ctx, "https://httpbin.org/patch");
      unless ($res->status == 200 && Fn->contains($res->content, "httpbin.org/patch")) {
        diag;
        return 0;
      }
    }

    # DELETE
    {
      my $res = $http->delete($ctx, "https://httpbin.org/delete");
      unless ($res->status == 200 && Fn->contains($res->content, "httpbin.org/delete")) {
        diag;
        return 0;
      }
    }

    return 1;
  }
  
  static method go : int () {
    
    $RESULT = IntList->new;
    
    my $ctx = Go::Context->background;
    my $http = HTTP::Tiny->new;
    
    # First goroutine
    Go->go([$ctx : Go::Context, $http : HTTP::Tiny] method : void () {
      my $url = "https://httpbin.org/get";
      my $res = $http->get($ctx, $url);
      my $content = $res->content;
      $RESULT->push(Fn->contains($content, "httpbin.org"));
    });
    
    # Second goroutine
    Go->go([$ctx : Go::Context, $http : HTTP::Tiny] method : void () {
      my $url = "https://httpbin.org/get";
      my $res = $http->get($ctx, $url);
      my $content = $res->content;
      $RESULT->push(Fn->contains($content, "httpbin.org"));
    });
    
    # Wait for all goroutines to finish
    Go->gosched;
    
    unless (Array->equals_int($RESULT->to_array, [1, 1])) {
      diag;
      return 0;
    }
    
    $RESULT = undef;
    
    return 1;
  }
}